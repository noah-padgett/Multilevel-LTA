TITLE: MLTA Test    

DATA: FILE IS condition1_rep1.dat;  

VARIABLE:	
	NAMES ARE u1-u4 u21-u24 c1 c2 cluster;     
	USEVARIABLES = u1-u4 u21-u24;
        CATEGORICAL = u1-u4 u21-u24;        
	CLASSES=c1(3) c2(3);        
	CLUSTER=cluster;        

ANALYSIS: TYPE = MIXTURE TWOLEVEL;        
	STARTS = 50 20;        
	PROCESS=4(STARTS);
	INTEGRATION = MONTE(500);    

MODEL:
    %WITHIN%
        %OVERALL%
            [c1#1]       (a01);
            [c1#2]       (a02);
            [c2#1@-15]   (a03);
            [c2#2@-15]   (a04);
            
            c2#1 ON c1#1   (g1);
            c2#2 ON c1#1   (g2);
            ! slope from c_1=2 to c_2=1 == 0
            c2#1 ON c1#2@0 (g3); ! needs to be 0
            c2#2 ON c1#2   (g4);
    %BETWEEN%
        %OVERALL%
            c2#1 ON c1#1 (b1);
            c2#2 ON c1#1 (b2);
            ! again slope => 0 can't go backways
            c2#1 ON c1#2@0 (b3);
            c2#2 ON c1#2 (b4);

            c1#1  (v1);
            c1#2  (v1);
            c2#1  (v2);
            c2#2  (v2);

    MODEL c1:
        %BETWEEN%
            %c1#1%
            [u1$1-u4$1](a1-a4);

            %c1#2%
            [u1$1-u4$1](a11-a14);

            %c1#3%
            [u1$1-u4$1](a21-a24);
  
    MODEL c2:
        %BETWEEN%
            %c2#1%
            [u21$1-u24$1](a1-a4);

            %c2#2%
            [u21$1-u24$1](a11-a14);

            %c2#3%
            [u21$1-u24$1](a21-a24);

MODEL CONSTRAINT:
    NEW(log11 log21 log12 log22 log13 log23
        prob11 prob21 prob31
        prob12 prob22 prob32
        prob13 prob23 prob33);
    ! define 4 logits. Note that logits are zero
    ! for the last (reference) c2 class
    ! logit c1=1, c2=1:
    log11 = a03 + b1*a01 + g1; ! = 2.88
    log21 = a03 + b2*a01 + g2; ! = 2.38
   
    log12 = a04 + b3*a02 + g3; ! = -15
    log22 = a04 + b4*a02 + g4; ! = 1.12
    
    log13 = a03; ! = -15
    log23 = a04; ! = -15
    
    ! define probabilities for the 4 c1, c2 cells
    prob11 = exp(log11)/(exp(log11)+exp(log21)+1);
    prob21 = exp(log21)/(exp(log11)+exp(log21)+1);
    prob31 = 1/(exp(log11)+exp(log21)+1);

    prob12 = exp(log12)/(exp(log12)+exp(log22)+1);
    prob22 = exp(log22)/(exp(log12)+exp(log22)+1);
    prob32 = 1/(exp(log12)+exp(log22)+1);

    prob13 = exp(log13)/(exp(log13)+exp(log23)+1);
    prob23 = exp(log23)/(exp(log13)+exp(log23)+1);
    prob33 = 1/(exp(log13)+exp(log23)+1);

SAVEDATA:       Results are result_condition1_dat1.par;
				FILE IS result_condition1_dat1.pro;
				SAVE IS CPROBABILITIES;

OUTPUT: tech1 tech4;